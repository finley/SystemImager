#!/usr/bin/perl -w
#
#    vi:set filetype=perl:
#
#    This file is part of SystemImager.
#
#    SystemImager is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 2 of the License, or
#    (at your option) any later version.
#
#    SystemImager is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with SystemImager. If not, see <https://www.gnu.org/licenses/>.
#
#    Copyright (C) 2003-2006 Brian Elliott Finley
#    Copyright (C) 2006 Andrea Righi
#

use strict;
use Carp;
use POSIX;
use File::Copy;
use File::Path;
use File::Basename;
use File::Glob;
use Getopt::Long;
use SystemImager::JConfig;
use SystemImager::Common;
use SystemImager::UseYourOwnKernel;
use vars qw($jconfig $VERSION);

# set version
$VERSION = "SYSTEMIMAGER_VERSION_STRING";

# set extension to use when backing up config files
my $backup_extension = ".before_systemimager-$VERSION";

# location of temporary rsyncd.conf file
my $rsyncd_conf_file = "/tmp/rsyncd.conf.$$";

# set path
$ENV{PATH} = "/bin:/usr/bin:/sbin:/usr/sbin";

# we rely on english text when parsing command output
$ENV{"LANG"} = "C";

my $progname = basename $0;

my $arch = SystemImager::UseYourOwnKernel::_get_arch();
my $autoinstall_boot_dir = $jconfig->get('pxe','boot_files');
unless ($autoinstall_boot_dir) {
    die "FATAL: parameter pxe.boot_files is not defined in ".$jconfig->fileName()."\n";
}

my $version_info = <<"EOF";
$progname (part of SystemImager) v$VERSION

Copyright (C) 1999-2006 Brian Elliott Finley
Copyright (C) 2006 Andrea Righi

This is free software; see the source for copying conditions.  There is NO
warranty; not even for MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
EOF

my $help_info = $version_info . <<"EOF";

Usage: $progname --destination DIR | --flavor FLAVOR [OPTION]...

Description:
        Takes the kernel specified, and necessary modules from it, and creates
        a new boot package based on said kernel.  The resultant files include
        a matched kernel and initrd.img that can be used as the SystemImager
        autoinstall client software.

Options:
 --version             
    Display version and copyright information.

 -h, --help                
    Display this output.

 --flavor FLAVOR
    The destination flavor to put the new boot package (kernel and
    initrd.img). This will create or overwrite a new boot flavor in 
    $autoinstall_boot_dir.

 --destination DIR
    The destination directory to put the new boot package (kernel and
    initrd.img).

 -y, --yes                 
    Answer yes to all yes/no questions.

 -q, --quiet
    Run silently.  Return an exit status of 0 for success or a non-zero
    exit status for failure.

 --my-modules
    Only include the modules that are currently loaded on this machine
    when creating the UYOK (Use Your Own Kernel) initrd.img.

    Without this option, all available modules are included whether or
    not they are currently in use (except for those listed in the
    /etc/systemimager/UYOK.modules_to_exclude file).

    Including all modules allows your UYOK kernel and initrd.img to be
    used with a greater variety of client hardware, but can make the
    resultant initrd.img much larger. 

 --kernel KERNEL
    Do not detect the kernel to use with UYOK, but instead use the
    file specified by the argument KERNEL.
    If --kernel is not specified, the running kernel will be used if
    its modules are available. Otherwise, the hight version in /boot
    will be used.

 --kver VERSION
    Do not detect the kernel to use with UYOK, but instead use the
    kernel with the specified version if available in /boot.

 --modules-dir DIR
    Get kernel modules to use with UYOK from the DIR repository.
    Use this parameter only with --kernel and only if you use UYOK
    feature.

 --firmware-dir DIR
    Add the firmwares to use from the DIR repository.

 --image IMAGENAME
    Get kernel and kernel modules automatically from the image specified
    by IMAGENAME to use with UYOK (if executed on image server).
    Use this parameter only if you use UYOK feature.

 --ssh-key FILE
    Where FILE is the SSH2 id_dsa or id_rsa private key of the user account
    that the autoinstall client will use to connect to the imageserver.

 --authorized-keys FILE
    Specifies the file that contains the public keys that can be used
    for user authentication from the image server to the imaging clients.

 --config FILE
    FILE can contain all the installation parameters necessary for the client
    (i.e. hostname, network configuration, image server address, log server,
    etc.).  This file is copied to /local.cfg into the initrd.img.  See
    SYSTEMIMAGER_DOC_DIR/local.cfg for a well commented example.
    In general any installation parameter passed via DHCP can be defined
    in local.cfg as well.

 --dracut-opts "options to add to dracut"
    this is the way to add somme parameters to the dracut command that will
    generate the image. e.g. --mdadmconf --strip --xz --uefi

Download, report bugs, and make suggestions at:
http://systemimager.org/

EOF

GetOptions(
    "help|h"                 => \my $help,
    "flavor=s"             => \my $flavor,
    "quiet"                => \my $quiet,
    "destination=s"        => \my $destination,
    "my-modules"           => \my $my_modules,
    "kernel=s"             => \my $custom_kernel,
    "kver=s"               => \my $kernel_version,
    "modules-dir=s"        => \my $modules_dir,
    "image=s"              => \my $image,
    "version"              => \my $version,
    "yes"                  => \my $yes,
    "ssh-key=s"            => \my $ssh_key,
    "authorized-keys=s"    => \my $authorized_keys,
    "config=s"             => \my $local_cfg,
    "firmware-dir=s"       => \my $firmware_dir,
    "dracut-opts=s"        => \my $dracut_opts,
) || die "$help_info";


### BEGIN option validation ###
# show version if requested
if($version) {
    print $version_info;
    exit 0;
}

# give help if requested
if($help) {
    print "$help_info";
    exit 0;
}

# bail if not root
if ($> != 0) {
    print "Must be run as root!\n";
    exit 1;
}

unless (($destination) or ($flavor)) {
    print "FATAL: missing --destination DIR, or --flavor FLAVOR option!\n";
    print qq(Try "$progname --help" for more info.\n);
    exit 1;
}

if (($destination) and ($flavor)) {
    print "FATAL: couldn't specify both --destination DIR and --flavor FLAVOR options!\n";
    print qq(Try "$progname --help" for more info.\n);
    exit 1;
}

if (!($custom_kernel) and $modules_dir) {
    print "FATAL:  --modules-dir DIR option must be used in conjunction with --kernel KERNEL!\n";
    print qq(Try "$progname --help" for more info.\n);
    exit 1;
}

if ($kernel_version and ! glob("/boot/vmlinu{x,z}-$kernel_version /lib/modules/$kernel_version/vmlinu{x,z}")) {
    print "FATAL: --kver=$kernel_version invalid! /boot/vmlinu{x,z}-$kernel_version or /lib/modules/$kernel_version/vmlinu{x,z} doesn't exists!\n";
    print "Make sure /boot/vmlinuz-$kernel_version or /lib/modules/$kernel_version/vmlinu{x,z} is available!"; # TODO: list available versions.
    exit 1;
}

if ($custom_kernel and !($modules_dir)) {
    print "WARNING: --kernel KERNEL used without --modules-dir DIR option!\n";
    print qq(You can use this option only if KERNEL is a monolithic kernel,\n) .
          qq(or if the needed modules are already present in the initrd\n) .
          qq(template (e.g. the standard BOEL kernel).\n\n);
    unless (($quiet) or ($yes)) {
        print "Continue? (y/[n]): ";
        my $answer = <>;
        unless($answer =~ /y/i) {
            print "Boot package build cancelled.  No files modified.\n";
            exit 1;
        }
    }
}

if ($my_modules and $modules_dir) {
    print "FATAL:  --my-modules option conflicts with --modules-dir DIR!\n";
    print qq(Try "$progname --help" for more info.\n);
    exit 1;
}

if ($custom_kernel and $image) {
    print "FATAL:  --kernel KERNEL option conflicts with --image IMAGENAME!\n";
    print qq(Try "$progname --help" for more info.\n);
    exit 1;
}

if ($my_modules and $image) {
    print "FATAL:  --my-modules option conflicts with --image IMAGENAME!\n";
    print qq(Try "$progname --help" for more info.\n);
    exit 1;
}

if($firmware_dir) {
    # System firmwares are loaded by default by dracut.
    # This options adds additionnal firmwares if needed.
    # Check that firmware_dir value points to a directory.
    if( ! -d $firmware_dir) {
        print "FATAL:  firmware dir $firmware_dir not found!\n";
        exit 1;
    }
}
#Â If firmware_dir is undefined, leave it undefined, the case is handeled in UseYourOwnKernel.pm

# if quiet redirect all to /dev/null
if ($quiet) {
    open(STDERR, '>/dev/null');
    open(STDOUT, '>/dev/null');
}

# Do UYOK stuff.
create_boot_package();

print << "EOF";

Boot kernel and initrd.img (generated by the initrd_template package)
can be found in $destination.

EOF

# Well done.
exit(0);

### BEGIN functions
# SystemImager specific functions

# Usage:
# create_boot_package();
sub create_boot_package {
    my $verbose = 1 unless($quiet);

    unless ($destination) {
        $destination = "$autoinstall_boot_dir/$arch/$flavor";
    }
    print "Creating the boot package in: $destination\n";

    # Check if the boot package already exists.
    if ((-e "$destination/kernel") or (-e "$destination/initrd.img")) {
        print "WARNING: a boot package (kernel + initrd.img) already exists in $destination!\n";
        print "If you choose to continue the old boot package will be overwritten!!!\n";
        unless (($quiet) or ($yes)) {
            print "Continue? (y/[n]): ";
            my $answer = <>;
            unless($answer =~ /y/i) {
                print "Boot package build cancelled.  No files modified.\n";
                exit 1;
            }
        }
    }
    # Removed: $filesystem system_firmware
    SystemImager::UseYourOwnKernel->create_uyok_initrd(
        $my_modules, $custom_kernel, $kernel_version,
        $modules_dir, $image, $destination,
        $ssh_key, $authorized_keys, $local_cfg,
        $firmware_dir, $dracut_opts, $verbose
    );

    return 1;
}

### END functions

# /* vi: set filetype=perl ai et ts=4: */

